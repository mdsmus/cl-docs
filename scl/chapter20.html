<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html><head>
<title>Successful Lisp - Chapter 20</title>
</head> 

<body bgcolor="white" text="black">
<h1>Chapter 20 - Macro Etiquette</h1> 

<P>Macros in Lisp are much more capable than macros in other
programming languages. Rather than just providing a simple shorthand
notation, Lisp macros give you the capability to truly extend the
language. In this chapter we'll learn about the program transforming
capabilities of macros as we see how to properly construct macros
to solve a wide variety of problems.

<h2>Macros are programs that generate programs</h2>

<p>Mention macros to most programmers, perhaps even you, and the
first image that comes to mind is string substitution -- you use a
macro to glue together a few parameters in a new way, maybe with a
bit of compile-time decision making thrown in. And because of the
typical (in languages other than Lisp) disparity between the macro
language and the programming language, the difficulty of writing a
macro increases much faster than its complexity.

<p>Lisp macros are Lisp programs that generate other Lisp programs.
The generated Lisp code has a fully-parenthesized notation, as does
the macro that generates the code. In the simplest case, a macro
substitutes forms within a template, clearly establishing a visual
correspondence between the generating code and the generated code.
Complex macros can use the full power of the Lisp language to
generate code according to the macro parameters; often a template
form is wrapped in code that constructs appropriate subforms, but
even this approach is just a typical use pattern and not a
requirement (or restriction) of the Lisp macro facility.

<p>In the following sections, we'll examine the mechanism by which
the Lisp system translates code generated by a macro, then we'll see
several increasingly sophisticated examples of macros. We'll finish
this chapter with a comparison of macros versus the use of inline
function declarations.

<h2>Close up: how macros work</h2>

<p>You define a macro with a <code>DEFMACRO</code> form, like this:

<pre>
(defmacro <i>name</i> (<i>arguments</i> ...)
  <i>body</i>)
</pre> 

<p><code>DEFMACRO</code> is like <code>DEFUN</code>, but instead of
returning values, the body of the <code>DEFMACRO</code> returns a
Lisp form. (As we'll see shortly, there's a very simple way to generate
this form with selected subforms replaced by parameters from the macro
call or computed by the macro's program.)

<p>Your program "calls" a macro the same way it calls a function,
but the behavior is quite different. First, none of the macro's
parameters are evaluated. Ever. Macro parameters are bound literally
to the corresponding arguments in the macro definition. If you pass
<code>(* 7 (+ 3 2))</code> to a macro, the argument in the body of
the macro definition is bound to the literal list <code>(* 7 (+ 3
2))</code>, and <em>not</em> the value 35.

<p>Next, the <em>macro expander</em> is invoked, receiving all of
the actual parameters bound to their corresponding arguments as
named by the <code>DEFMACRO</code> form. The macro expander is just
the body of the <code>DEFMACRO</code> form, which is just Lisp code;
the only catch is that the Lisp system expects the macro expander to
return a Lisp form.

<p>The Lisp system then evaluates whatever form the macro expander
returns. If the returned form is a macro, it gets expanded.
Otherwise, the form is evaluated by the rules we first learned in <a
href="chapter03-02.html">Chapter 3, Lesson 2</a>.

<blockquote>
The preceding paragraph is conceptually correct. However, a Lisp 
implementation may expand macros at different times. A macro could be
expanded just once, when your program is compiled. Or it could be 
expanded on first use as your program runs, and the expansion could
be cached for subsequent reuse. Or the macro could be expanded every
time it's used. A properly written macro will behave the same under
all of these implementations.
</blockquote> 

<p>In <a href="chapter21.html">Chapter 21</a> we'll expand upon some
of the things you can express with argument lists.

<h2>Backquote looks like a substitution template</h2>

<p>The simplest way to generate a form in the body of your macro
expander is to use the <em>backquote</em> (<code>`</code>) reader
macro. This behaves like the <em>quote</em> (<code>'</code>) reader
macro, except for when a comma (<code>,</code>) appears in the
backquoted form.

<blockquote>
A comma is only permitted in a backquoted form. If you use a comma
in a quoted form, Lisp will signal an error when it reads the form.
</blockquote> 

<p>Like quote, backquote suppresses evaluation. But a comma within a
backquoted form "unsuppresses" evaluation for just the following
subform.

<pre>
? `(The sum of 17 and 83 is ,(+ 17 83))
(THE SUM OF 17 AND 83 IS 100)
</pre> 

<p>Compare the preceding example, which used backquote, with the
similar form using quote (and omitting the comma).

<pre>
? '(The sum of 17 and 83 is (+ 17 83))
(THE SUM OF 17 AND 83 IS (+ 17 83))
</pre> 

<p>You can probably imagine how backquote and comma provide a template
with substitution capabilities. This is just what we need for our
macro expander. Here are a couple of simple examples.

<pre>
<i>; Define the macro</i> 
? (defmacro swap (a b) <i>; NOTE: This is a restricted version of ROTATEF</i> 
    `(let ((temp ,a))
       (setf ,a ,b)
       (setf ,b temp)))
SWAP

<i>; First invocation</i> 
? (let ((x 3)
        (y 7))
    (swap x y) <i>; macro call</i> 
    (list x y))
(7 3)
<i>; Let's see the form generated by SWAP:</i> 
? (pprint (macroexpand-1 '(swap x y)))

(LET ((TEMP X))
  (SETF X Y)
  (SETF Y TEMP))

<i>; Second invocation</i> 
? (let ((c (cons 2 9))) <i>; (2 . 9)</i> 
    (swap (car c) (cdr c))
    c)
(9 . 2)
<i>; And the expansion of its macro call</i> 
? (pprint (macroexpand-1 '(swap (car c) (cdr c))))

(LET ((TEMP (CAR C)))
  (SETF (CAR C) (CDR C))
  (SETF (CDR C) TEMP))

<i>; Here's the second invocation again, "macroexpanded" by hand.</i> 
? (let ((c (cons 2 9)))
    (LET ((TEMP (CAR C)))
      (SETF (CAR C) (CDR C))
      (SETF (CDR C) TEMP))
    c)
(9 . 2)
</pre> 

<p><code>(PPRINT (MACROEXPAND-1 '<i>macro-call</i>))</code> is a very
handy tool to see what form your macro expander generates. (Don't worry
if the output from your Lisp system looks exactly as shown here; there
may be some differences in layout.)

<p>As you look at these examples, the important things to note are that:

<ol>
<li>the macro arguments receive the literal representation of their actual
parameters from the macro call, and

<li>macro arguments that are preceded by a comma within a backquoted form
are substituted with the literal representation of the parameter from the
macro call.
</ol> 

<p>Here are some more macro definitions. Experiment with these in your
Lisp system to see what they do.

<pre>
(defmacro sortf (place)
  `(setf ,place (sort ,place)))

(defmacro togglef (place)
  `(setf ,place (not ,place)))

(defmacro either (form1 form2)
  <i>; (random 2) returns 0 or 1</i> 
  `(if (zerop (random 2)) ,form1 ,form2))
</pre> 

<h2>Beyond the obvious, part 1: compute, then generate</h2>

<p>Macros start to get interesting when they do more than a simple
textual substitution. In this section, we'll explore a real-world
example of using a macro to extend Lisp into the problem domain. In
addition to providing a macro expander, our new macro will
automatically generate an environment that will be referenced by the
expander. Our example will show how to move computations from
run-time to compile-time, and how to share information computed at
compile-time.

<p>Let's say you're working on an interactive game that makes heavy
use of the trigonometric function <em>sine r</em> in computing player
motion and interaction. You've already determined that calling the
Lisp function <code>SIN</code> is too time-consuming; you also know
that your program will work just fine with approximate results for
the computation of <em>sine r</em>. You'd like to define a
<code>LOOKUP-SIN</code> macro to do the table lookup at runtime;
you'd also like to hide the details of table generation, an
implementation detail with which you'd rather not clutter your
program's source code.

<p>Your macro will be invoked as <code>(LOOKUP-SIN</code>
<var>radians</var> <var>divisions</var><code>)</code>, where
<var>radians</var> is always in the range of zero to one-quarter pi,
and <var>divisions</var> is the number of discrete values available
as the result of <code>LOOKUP-SIN</code>. At runtime, the macro
expander will just compute the index into a lookup table, and return
the value from the table. The table will be generated at
compile-time (on most Lisp systems). Furthermore, only one table
will ever be generated for a given value of <var>divisions</var> in
the macro call.

<p>Here's the code. The comments and documentation strings should
help you to understand the code as you read it. I'll provide further
explanation below.

<pre>
<i>;; This is where we cache all of the sine tables generated</i> 
<i>;; during compilation. The tables stay around at runtime</i> 
<i>;; so they can be used for lookups.</i> 
(defvar *sin-tables* (make-hash-table)
  "A hash table of tables of sine values. The hash is keyed
by the number of entries in each sine table.")

<i>;; This is a helper function for the LOOKUP-SIN macro.</i> 
<i>;; It is used only at compile time.</i> 
(defun get-sin-table-and-increment (divisions)
  "Returns a sine lookup table and the number of radians quantized
by each entry in the table. Tables of a given size are reused.
A table covers angles from zero to pi/4 radians."
  (let ((table (gethash divisions *sin-tables* :none))
        (increment (/ pi 2 divisions)))
    (when (eq table :none)
      <i>;; Uncomment the next line to see when a table gets created.</i> 
      <i>;;(print '|Making new table|)</i> 
      (setq table
            (setf (gethash divisions *sin-tables*)
                  (make-array (1+ divisions) :initial-element 1.0)))
      (dotimes (i divisions)
        (setf (aref table i)
              (sin (* increment i)))))
    (values table increment)))

<i>;; Macro calls the helper at compile time, and returns an</i> 
<i>;; AREF form to do the lookup at runtime.</i> 
(defmacro lookup-sin (radians divisions)
  "Return a sine value via table lookup."
  (multiple-value-bind (table increment)
                       (get-sin-table-and-increment divisions)
    `(aref ,table (round ,radians ,increment))))
</pre> 

<p>If you still don't see the point of all this code after having
read the introduction to this section and the comments in the code,
here it is: when your program runs, it executes <em>just</em>
<code>AREF</code> (and associated <code>ROUND</code>) to look up the
<em>sin r</em> value.

<pre>
? (pprint (macroexpand-1 '(lookup-sin (/ pi 4) 50)))

(AREF #(0.0 0.0314107590781283 0.06279051952931337 
        <i>[additional entries not shown]</i> 
        0.9980267284282716 0.9995065603657316 1.0)
      (ROUND (/ PI 4) 0.031415926535897934))
<i>;; Note that the macro call makes no mention of a lookup table.</i> 
<i>;; Tables are generated as-needed by (and for) the compiler.</i> 
? (lookup-sin (/ pi 4) 50)
0.7071067811865476
</pre> 

<p>In the macroexpansion, the <code>#(...)</code> is the printed
representation of the lookup table for 50 divisions of the quarter
circle. This table is stored in the <code>*SIN-TABLES*</code> hash
table, where it is shared by <em>every</em> macro call to
<code>(LOOKUP-SIN</code> <var>angle</var> <code>50)</code>. We don't
even have to do a hash lookup at runtime, because the macro expander
has captured the free variable <code>TABLE</code> from the
<code>MULTIPLE-VALUE-BIND</code> form in <code>LOOKUP-SIN</code>.

<h2>Beyond the obvious, part 2: macros that define macros</h2>

<p>Macros that define macros are used infrequently, partly because
it's hard to think of a good use for this technique and partly
because it's difficult to get right. The following macro, based upon
an example in <a href="chapter33.html#graham94">Paul Graham's "On
Lisp"</a> book, can be used to define synonyms for the names of Lisp
functions, macros, and special forms.

<pre>
? (defmacro defsynonym (old-name new-name)
    "Define OLD-NAME to be equivalent to NEW-NAME when used in
the first position of a Lisp form."
    `(defmacro ,new-name (&amp;rest args)
       `(,',old-name ,@args)))
DEFSYNONYM
? (defsynonym make-pair cons)
MAKE-PAIR
? (make-pair 'a 'b)
(A . B)
</pre> 

<p>Macros are always a little bit dangerous because code containing a macro
call does not automatically get updated if you change the definition of the
macro. You can always establish your own convention to help you remember
that you need to recompile certain code after you change a macro definition.
But there's always the possibility that you'll forget, or make a mistake.

<p>Ultimately, the likelihood that you'll inadvertently end up with
code that was compiled with an old version of a macro is directly
proportional to how often you're likely to change the macro. I'll
probably never need to change the <code>LOOKUP-SIN</code> macro from
the previous section once it's defined and working. On the other
hand, a macro like <code>DEFSYNONYM</code> practically begs to be
used again and again as you generate new code. If you change your
mind about the <em>old name</em> to associate with a given <em>new
name</em>, all of your previously compiled code will still refer to
the <code>old name</code> that you had decided upon previously.

<pre>
<i>;; WARNING: This example illustrates a practice to avoid!</i> 

<i>;; Here's some core algorithm</i> 
? (defun process-blah-using-algorithm-zark (...) ...)
PROCESS-BLAH-USING-ALGORITHM-ZARK

<i>;; And here's where I use the algorithm, perhaps calling it</i> 
<i>;; from many other places in DO-STUFF besides the one I've shown.</i> 
? (defun do-stuff (...)
    ...
    (process-blah-using-algorithm-zark ...)
    ...)
DO-STUFF
<i>;; Try it out...</i> 
? (do-stuff ...)
[results based upon process-blah-using-algorithm-zark]
<i>;; OK, this looks good.  But I think I'll clean up the</i> 
<i>;; appearance of DO-STUFF by defining an abbreviation</i> 
<i>;; for that really long core algorithm name.</i> 
? (defsynonym process-blah-using-algorithm-zark proc)
PROC
<i>;; Now I'll rewrite DO-STUFF to use the abbreviation.</i> 
? (defun do-stuff (...)
    ...
    (proc ...)
    ...)
DO-STUFF
<i>;; And make sure it still works.</i> 
? (do-stuff ...)
[results based upon process-blah-using-algorithm-zark]

<i>... Some time later ...</i> 

<i>;; Oh, here's a better core algorithm.</i> 
? (defun process-blah-using-algorithm-glonkfarkle (...) ...)
PROCESS-BLAH-USING-ALGORITHM-GLONKFARKLE
<i>;; I'll change the synonym for PROC to 'be' the new algorithm.</i> 
? (defsynonym process-blah-using-algorithm-glonkfarkle proc)
PROC

<i>... Some time later ...</i> 

<i>;; Time to use DO-STUFF again...</i> 
? (do-stuff ...)
[results based upon process-blah-using-algorithm-zark]
<i>;; Hey!! These results don't seem to use the new algorithm.</i> 
<i>;; What could be wrong?  The code LOOKS right...</i> 
</pre> 

<p>The problem, of course, is that the second use of
<code>DEFSYNONYM</code> redefined the <code>PROC</code> macro, and I
didn't notice that <code>DO-STUFF</code> needed to be recompiled to
pick up the changed definition.

<blockquote> My advice: Don't try to be clever by using macros like
<code>DEFSYNONYM</code>. Stick with descriptive names that are as
long as necessary, and use an editor that supports symbol completion
(see <a href="chapter27.html">Chapter 27</a>). Remember, there's
only one way to <em>not</em> abbreviate a name; using abbreviations
increases the chance that you'll use the wrong one. </blockquote> 

<h2>Tricks of the trade: elude capture using GENSYM</h2>

<p>You have to be be careful when you define a macro that introduces
new variables in its expansion. The <code>REPEAT</code> macro, below,
offers us a shorthand way of repeating a body of code a certain number
of times.

<pre>
? (defmacro repeat (times &amp;body body)
    `(dotimes (x ,times)
       ,@body))
REPEAT
? (repeat 3 (print 'hi))

HI
HI
HI
NIL
</pre> 

<p>This seems to do the right thing, but the variable <code>X</code> is
going to cause problems.  The following example <em>should</em> give us
the same results as the last example.

<pre>
? (setq x 'hi)
HI
? x
HI
? (repeat 3 (print x))

0
1
2
NIL
</pre> 

<p>The variable <code>X</code> in the macro expander
<code>shadowed</code> the global <code>X</code> that we tried to
reference in the body. Another way to say this is that
<code>X</code> is <em>free</em> in the body of the
<code>REPEAT</code> form, but it was <em>captured</em> by the
definition of <code>X</code> in the macro expander; this prevents
the body form from reaching the intended variable <code>X</code>.

<p>The obvious solution is to use a different variable name in the
macro expander -- one that won't conflict with any name we'll ever
use in our code that calls the <code>REPEAT</code> macro. You might
think that some kind of naming convention would work, but there's 
always the chance that some programmer will come along later and
violate the convention.  We need a foolproof approach.

<p>Lisp provides a <code>GENSYM</code> function to generate symbols
that are <em>guaranteed</em> to be unique. No programmer can ever
write a symbol name that conflicts with a symbol created by
<code>GENSYM</code>. Here is how we use <code>GENSYM</code> to
create a name for the variable needed in the macro expander for the
<code>REPEAT</code> macro.

<pre>
? (defmacro repeat (times &amp;body body)
    (let ((x (gensym)))
      `(dotimes (,x ,times)
         ,@body)))
REPEAT
? x
HI
? (repeat 3 (print x))

HI
HI
HI
NIL
</pre> 

<p>With this new <code>REPEAT</code> macro, we compute a new symbol
in the <code>LET</code> form, and substitute this symbol in the
macro expander form. To see why this works, let's look at an expansion:

<pre>
? (macroexpand-1 '(repeat 5 (print x))
(DOTIMES (#:G8524 5) (PRINT X))
</pre> 

<p><code>#:G8524</code> is a unique <em>uninterned</em> symbol.  You can
see that it's uninterned by the <code>#:</code> prefix. But how does Lisp
guarantee the uniqueness of this symbol?  The Lisp reader guarantees that
any symbol it reads with the <code>#:</code> prefix is unique.  Compare
the following:

<pre>
? (eq 'a 'a)
T
? (eq '#:a '#:a)
NIL
</pre> 

<p>Even though the <code>#:A</code> symbols print the same, they are
different.

<p>Generating variable names to be used in macro expanders has another 
application. This next macro definition has a subtle problem:

<pre>
? (defmacro cube (n)
    `(* ,n ,n ,n))
CUBE
? (cube 3)
27
? (let ((n 2))
    (cube (incf n)))
60
</pre> 

<p>In the second case, <code>(INCF N)</code> should have provided the 
value 3 to <code>CUBE</code> and the result should have been identical
to the first test. Let's take a look at the expansion again, to see
what happened.

<pre>
? (macroexpand-1 '(cube (incf n)))
(* (INCF N) (INCF N) (INCF N))
</pre> 

<p>The problem is obvious: <code>CUBE</code>'s argument, <code>(INCF
N)</code> is being evaluated multiple times. As a rule, this is a bad
thing to do, because it violates our assumptions about the way Lisp
evaluates forms. We fix this problem by arranging for the macro expander
to evaluate <code>CUBE</code>'s argument just once.

<pre>
? (defmacro cube (n)
    (let ((x (gensym)))
      `(let ((,x ,n))
         (* ,x ,x ,x))))
CUBE
? (let ((n 2))
    (cube (incf n)))
27
</pre> 

<p>We created a unique symbol outside of the macro expander, then
used this symbol in the expander as the name of a variable to hold
the result of evaluating <code>CUBE</code>'s argument. The
<code>LET</code> form in the macro expander is the <em>only</em>
place where <code>CUBE</code>'s argument is referenced, so it gets
evaluated exactly once for each call to <code>CUBE</code>.

<h2>Macros vs. inlining</h2>

<p>Lisp allows functions to be inlined by the compiler. In other words,
rather than compiling a call to the function, the compiler may substitute
the function's body, thus saving the overhead of a function call.
Substituting the function's body is generally expensive in terms of space,
since a function body's code is usually longer than the code of its 
calling sequence.

<p>It's important to understand that Lisp <em>allows</em> functions to
be inlined. Like all other declarations -- save the <code>SPECIAL</code> 
declaration -- an <code>INLINE</code> declaration may be treated as
advisory or ignored entirely by the compiler.

<p>Here are some examples of how to inline a function.  In the first case, 
function <code>F</code> is inlined everywhere it is used (assuming that
the compiler supports inlining).  In the second case, function <code>P</code> 
is compiled with information to support inlining, but is only inlined
in the presence of a declaration, as in function <code>Q</code>.

<pre>
<i>; Case 1 -- F may always be inlined</i> 
(declaim (inline f))
(defun f (...) ...)

(defun g (...)
  (f ...)
  ...)

(defun h (...)
  (f ...)
  ...)

<i>; Case 2 - P may be inlined only following a declaration</i> 
(declaim (inline p))
(defun p (...) ...)
(declaim (notinline p))

(defun q (...) 
  (declare (inline p))
  (p ...) <i>; inlined</i> 
  ...)

(defun r (...)
  (p ...) <i>; not inlined</i> 
  ...)
</pre> 

<p>Macros may be used in place of <code>INLINE</code> declarations for
cases where code absolutely <em>must</em> be inlined despite the presence
(or absence) of compiler support for inlining. 

<p>In general, though, you should use macros for language extension,
and not for efficiency hacks. The risk of forgetting to recompile
after having changed a macro definition can cause hard-to-find bugs
that will waste a lot of your development effort.

<blockquote> My advice: Don't use macros as a substitute for
inlining unless you can find no other way to achieve desired
performance; of course, such efforts should be guided by the results
of profiling your code (see <a href="chapter28.html"> Chapter
28</a>) and preferably only when your code is already stable and
debugged. You should also reexamine your decision with each new
release of your Lisp compiler, and whenever you port your program to
another Lisp platform. </blockquote> 

<hr>
<div align="center">
<a href="contents.html">Contents</a> | <a href="cover.html">Cover</a> <br> 
<a href="chapter19.html">Chapter 19</a> | Chapter 20 | <a href="chapter21.html">Chapter 21</a> 
</div>
<hr>
<address>
Copyright &copy; 1995-2001, David B. Lamkins<br>
All Rights Reserved Worldwide<br>
<br>
This book may not be reproduced without the written consent of its author.  Online distribution is restricted to the author's site.
</address> 
</body> </html> 